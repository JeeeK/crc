500 CLEAR 512
1000 REM J.KLASEK J@KLASEK.AT
1010 REM 2012-05-08, 2023-05-15
1020 REM DRAGON/COCO VERSION
1100 DIMC,I,V,P,IM,IP,IA
1105 REM XOR
1106 DEF FN XR(X)=(C OR X)-(C AND X)
1108 REM INTEGER CORRECTION (16-BIT SIGNED)
1109 DEF FN IC(X)=X+(X>IP)*IA
1110 HE$="0123456789ABCDEF"
1111 REM FLOAT CORRECTION (16-BIT SIGNED)
1112 DEF FN FC(X)=X-(X<.)*IA
1120 IM=-32768:IP=32767:IA=65536
1200 GOTO 10000
1900 REM CALCULATING WITH SIGNED INTEGERS
1901 REM E.G. 3-BIT RANGE:
1911 REM -4   -3   -2   -1   0   1   2   3
1912 REM 100 101  110  111  000 001 010 011
1913 REM < 0 -> HSB SET
1914 REM < -4 -> OVERFLOW, CORRECT WITH +8
1915 REM   -3+-3=-6 (1010) -> 2 (010)
1916 REM > 3 -> OVERFLOW, CORRECT WITH -8
1917 REM   3+3=6 (110) -> -2 (010)
1920 REM
1921 REM SAME FOR 16 BIT FROM -32768 TO +32767
1998 END
1999 REM CRC16 SUBROUTINE
2000 REM POLYNOM $1021, TO 16-BIT INTEGER RANGE:
2005 P=1*4096+2*16+1: P=FN IC(P)
2006 C=0
2010 PRINT "POLYNOM:"FNFC(P)" $"HEX$(FNFC(P))
2011 PRINT "START-CRC:"FNFC(C)" $"HEX$(FNFC(C))
2050 FOR A=S TOS+N-1
2060 :V=FN IC(PEEK(A)*256):REM GET MEMORY, TO HIGH BYTE
2080 :REM C=(CORV)-(CANDV)
2085 :C=FN XR(V)
2095 :REM PRINT A;PEEK(A);V," CRC=";C
2100 FORB=1TO8
2110 : IF C<0 THEN 2300
2115 :  REM ONLY SHIFT LEFT
2120 :  C=C+C:IF C>IP THEN C=C-IA
2130 : GOTO 2400
2290 :  REM SHIFT LEFT
2300 :  C=C+C:IF C<IM THEN C=C+IA
2305 :  REM DIVIDE BY POLYNOM ...
2310 :  REM C=(CORP)-(CANDP)
2320 :  C=FN XR(P)
2400 : NEXT
2500 NEXT
2900 RETURN
9999 REM
10000 REM S=START, N=ANZAHL
10010 S=32768-1024: REM MEMORY WHERE TO WRITE THE DATA ...
10015 DATA "@ZYXWVUTSRQPONMLKJIHGFEDBCA"
10016 REM X-MODEM CRC=$B199
10017 DATA "@A@N @A@RBITRARY @S@TRING"
10018 REM X-MODEM CRC=$DDFC
10029 DATA ""
10030 GOTO 10080
10040 GOSUB 11000: REM RETURNS N
10050 T=TIMER:GOSUB 2000:PRINT "ELAPSED TIME:"(TIMER-T)/50
10070 PRINT "CRC16=";FNFC(C);" $"HEX$(FNFC(C)):PRINT
10080 READ D$: IF D$<>"" THEN 10040
10998 END
10999 REM MOVE DATA FROM STRING TO MEMORY
11000 N=LEN(D$):PRINT "DATA: "D$
11010 AC=32: REM START WITH ASCII LOWERCASE
11015 DO$="":M=S
11020 FOR I=1TON
11030 A=ASC(MID$(D$,I,1))AND127
11040 IF A=64 THEN AC=32-AC:GOTO11100
11050 DO$=DO$+CHR$(A-(A>64)*AC)
11060 A=(A-AC*(A>64ANDA<91))
11090 POKE M,A:M=M+1
11100 NEXT
11110 PRINT:PRINT DO$;:N=LEN(DO$):PRINT ", N:"N
11120 RETURN
